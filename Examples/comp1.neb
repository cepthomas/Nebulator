

///// Include other lib files. /////
#import "utils.neb"
#import "scale.neb"


// Volumes
const double KEYS_VOL = 0.8;
const double DRUM_VOL = 1.0;
const double BASS_VOL = 1.0;


///// Channels /////
NChannel chKeys;
NChannel chBass;
NChannel chDrums;
NChannel chSynth;

// Use wingm and virtual kbd only.
bool _local = true;


////// Nebulator midi stuff //////
public override void Setup()
{
    string midiout = _local ? "MIDI:Microsoft GS Wavetable Synth" : "MIDI:loopMIDI Port 1";

    // IO devices.
    // All the outputs.
    chKeys = CreateChannel("keys", midiout, 1, 0.1);
    chBass = CreateChannel("bass", midiout, 2, 0.1);
    chSynth = CreateChannel("synth", midiout, 3, 0.1);
    chDrums = CreateChannel("drums", midiout, 10, 0.1);
}

public override void Setup2()
{
    if(_local)
    {
        SendPatch(chKeys, AcousticGrandPiano);
        SendPatch(chBass, AcousticBass);
        SendPatch(chSynth, Lead1Square);
        SendPatch(chDrums, 32); // kit
    }

    BuildComp();
}



//tock size = 32
//7mspb = 81.5bpm
//8mspb = 78bpm


//tock size = 96
//7mspb = 89bpm
//8mspb = 78bpm


void BuildComp()
{
    // Reaper Time is 1.1.0 to 1.4.99
    // One beat is 1/4 note.
    // One hit is 1/16 note.
    // 0.0 to 3.3
    // 0.0 0.1 0.2 0.3 1.0 1.1 1.2 1.3 2.0 2.1 2.2 2.3 3.0 3.1 3.2 3.3
    // ? or 0.0 to 3.7
    
    /////////////////////// Keys sequences /////////////////////////////////

    NSequence seqKeysIntro = CreateSequence(4, new NSequenceElements
    {
        //"|m...............|b...b...b...b...|iiiiiiiiiiiiiiii|
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", "A4.m", KEYS_VOL },
    });

    NSequence seqKeysVerse = CreateSequence(4, new NSequenceElements
    {
        { "|9-------------  |                |                |                |", "B4.m", KEYS_VOL },
        { "|                |9-------------  |                |                |", "A4.m", KEYS_VOL },
        { "|                |                |9---------------|----------      |", "E4.m", KEYS_VOL },
    });

    NSequence seqKeysEnd = CreateSequence(4, new NSequenceElements
    {
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", "A4.m", KEYS_VOL },
    });

    /////////////////////// Drum sequences /////////////////////////////////

    NSequence seqDrumsVerse = CreateSequence(4, new NSequenceElements
    {
        { "|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|", RideCymbal1,      DRUM_VOL*0.8 },
        { "|....8.......8...|....8.......8...|....8.......8...|....8.......8...|", SideStick,        DRUM_VOL*0.9 },
        { "|8.....8.8.......|8.....8.8.......|8.....8.8.......|8.....8.8.......|", AcousticBassDrum, DRUM_VOL },
    });

    /////////////////////// Bass sequences /////////////////////////////////

    NSequence seqBassIntro = CreateSequence(4, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });

    NSequence seqBassVerse = CreateSequence(4, new NSequenceElements
    {
        { "|9----.9.9------.|................|................|................|", "B2", BASS_VOL },
        { "|................|9----.9.9------.|................|................|", "A2", BASS_VOL },
        { "|................|................|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });

    NSequence seqBassEnd = CreateSequence(4, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });


    /////////////////////// sections /////////////////////////////////

    CreateSection(8, "Intro", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysIntro },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassIntro  },
    });

    CreateSection(16, "Middle", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysVerse },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassVerse  },
    });

    CreateSection(8, "End", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysEnd },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassEnd  },
    });
}
