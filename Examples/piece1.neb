

///// Include other lib files. /////
#import "utils.neb"
#import "scale.neb"


// Volumes
const double KEYS_VOL = 0.8;
const double DRUM_VOL = 1.0;
const double BASS_VOL = 1.0;
const double SYNTH_VOL = 1.0;


///// Channels /////
NChannel chKeys;
NChannel chBass;
NChannel chDrums;
NChannel chSynth;

// Use wingm and virtual kbd only.
bool _local = true;


////// Nebulator midi stuff //////
public override void Setup()
{
    string midiout = _local ? "MIDI:Microsoft GS Wavetable Synth" : "MIDI:loopMIDI Port 1";
    // string midiout = "MIDI:VirtualMIDISynth #1";

    // IO devices.
    // All the outputs.
    chKeys = CreateChannel("keys", midiout, 1, 0.1);
    chBass = CreateChannel("bass", midiout, 2, 0.1);
    chSynth = CreateChannel("synth", midiout, 3, 0.1);
    chDrums = CreateChannel("drums", midiout, 10, 0.1);
}

public override void Setup2()
{
    if(_local)
    {
        SendPatch(chKeys, AcousticGrandPiano);
        SendPatch(chBass, AcousticBass);
        SendPatch(chSynth, Lead1Square);
        SendPatch(chDrums, 32); // kit
    }

    BuildComp();
}

void BuildComp()
{
    // TODO - fills, intros, like that.
    
    /////////////////////// Keys sequences /////////////////////////////////

    NSequence seqKeysIntro = CreateSequence(16, new NSequenceElements
    {
        //"|m...............|b...b...b...b...|tttttttttttttttt|tttttttttttttttt|
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", "A4.m", KEYS_VOL },
    });

    NSequence seqKeysVerse = CreateSequence(16, new NSequenceElements
    {
        { "|9-------------  |                |                |                |", "B4.m", KEYS_VOL },
        { "|                |9-------------  |                |                |", "A4.m", KEYS_VOL },
        { "|                |                |9---------------|----------      |", "E4.m", KEYS_VOL },
    });

    NSequence seqKeysEnd = CreateSequence(16, new NSequenceElements
    {
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", "A4.m", KEYS_VOL },
    });

    /////////////////////// Drum sequences /////////////////////////////////

    NSequence seqDrumsVerse = CreateSequence(16, new NSequenceElements
    {
        { "|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|", RideCymbal1,      DRUM_VOL*0.8 },
        { "|....8.......8...|....8.......8...|....8.......8...|....8.......8...|", SideStick,        DRUM_VOL*0.9 },
        { "|8.....8.8.......|8.....8.8.......|8.....8.8.......|8.....8.8.......|", AcousticBassDrum, DRUM_VOL },
    });

    /////////////////////// Bass sequences /////////////////////////////////

    NSequence seqBassIntro = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });

    NSequence seqBassVerse = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|................|................|................|", "B2", BASS_VOL },
        { "|................|9----.9.9------.|................|................|", "A2", BASS_VOL },
        { "|................|................|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });

    NSequence seqBassEnd = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", "E2", BASS_VOL },
    });


    /////////////////////// Synth sequences /////////////////////////////////
    NSequence seqSynth1 = CreateSequence(16, new NSequenceElements
    {
        { "|9---------------|................|................|................|", "B4", SYNTH_VOL },
        { "|................|9---------------|................|................|", "A4", SYNTH_VOL },
        { "|................|................|9---------------|----------------|", "E5", SYNTH_VOL },
    });

    /////////////////////// sections /////////////////////////////////

    CreateSection(32, "Intro", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysIntro },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassIntro  },
    });

    CreateSection(64, "Middle", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysVerse },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassVerse  },
        { chSynth, Loop,  seqSynth1  },
    });

    CreateSection(32, "End", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysEnd },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassEnd  },
    });
}
