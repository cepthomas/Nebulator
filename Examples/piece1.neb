

///// Include other lib files. /////
#include "utils.neb"
#include "scale.neb"


// Volumes
const double KEYS_VOL = 0.8;
const double DRUM_VOL = 1.0;
const double BASS_VOL = 1.0;
const double SYNTH_VOL = 1.0;


///// Channels /////
NChannel chKeys;
NChannel chBass;
NChannel chDrums;
NChannel chSynth;


////// Nebulator midi stuff //////
public override void Setup()
{
    // IO devices.
    // All the outputs.
    chKeys =  CreateChannel("keys",  DeviceType.MidiOut, 1,  0.1);
    chBass =  CreateChannel("bass",  DeviceType.MidiOut, 2,  0.1);
    chSynth = CreateChannel("synth", DeviceType.MidiOut, 3,  0.1);
    chDrums = CreateChannel("drums", DeviceType.MidiOut, 10, 0.1);

    // All the inputs.
    //CreateController(DeviceType.VkeyIn, 1, NoteControl, vVkeyNote);
}
 
public override void Setup2()
{
    SendPatch(chKeys,  ElectricGrandPiano); // ElectricGrandPiano AcousticGrandPiano
    SendPatch(chBass,  AcousticBass);
    SendPatch(chSynth, Pad5Bowed);
    SendPatch(chDrums, 32); // drum kit

    // Do some panning. Apparently FluidR3 sf ignores CC for some patches like AcousticGrandPiano
    SendController(chKeys,  Pan, 30);
    SendController(chSynth, Pan, 100);

    BuildComp();
    
}

void BuildComp()
{
    
    /////////////////////// Keys sequences /////////////////////////////////

    NSequence seqKeysIntro = CreateSequence(16, new NSequenceElements
    {
        //"|m...............|b...b...b...b...|tttttttttttttttt|tttttttttttttttt|
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", 16, "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", 16, "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", 16, "A4.m", KEYS_VOL },
    });

    NSequence seqKeysVerse = CreateSequence(16, new NSequenceElements
    {
        { "|9-------------  |                |                |                |", 16, "B4.m", KEYS_VOL },
        { "|                |9-------------  |                |                |", 16, "A4.m", KEYS_VOL },
        { "|                |                |9---------------|----------      |", 16, "E4.m", KEYS_VOL },
    });

    NSequence seqKeysEnd = CreateSequence(16, new NSequenceElements
    {
        { "|9-------------  |9-------------  |9 9-----------  |9-----          |", 16, "E4.m", KEYS_VOL },
        { "|                |                |                |        9 9     |", 16, "D5.m", KEYS_VOL },
        { "|                |                |                |            9 9 |", 16, "A4.m", KEYS_VOL },
    });

    /////////////////////// Drum sequences /////////////////////////////////

    NSequence seqDrumsVerse = CreateSequence(16, new NSequenceElements
    {
        { "|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|8.8.8.8.8.8.8.8.|", 16, RideCymbal1,      DRUM_VOL*0.8 },
        { "|....8.......8...|....8.......8...|....8.......8...|....8.......8...|", 16, SideStick,        DRUM_VOL*0.9 },
        { "|8.....8.8.......|8.....8.8.......|8.....8.8.......|8.....8.8.......|", 16, AcousticBassDrum, DRUM_VOL },
    });

    /////////////////////// Bass sequences /////////////////////////////////

    NSequence seqBassIntro = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", 16, "E2", BASS_VOL },
    });

    NSequence seqBassVerse = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|                |                |                |", 16, "B2", BASS_VOL },
        { "|                |9----.9.9------.|                |                |", 16, "A2", BASS_VOL },
        { "|                |                |9----.9.9------.|9----.9.9------.|", 16, "E2", BASS_VOL },
    });

    NSequence seqBassEnd = CreateSequence(16, new NSequenceElements
    {
        { "|9----.9.9------.|9----.9.9------.|9----.9.9------.|9----.9.9------.|", 16, "E2", BASS_VOL },
    });


    /////////////////////// Synth sequences /////////////////////////////////
    NSequence seqSynth1 = CreateSequence(16, new NSequenceElements
    {
        { "|9------        7|                |               7|                |", 16, "B4", SYNTH_VOL },
        { "|                |9-------       7|                |                |", 16, "A4", SYNTH_VOL },
        { "|                |                |9-------        |5------------  5|", 16, "E5", SYNTH_VOL },
    });

    /////////////////////// sections /////////////////////////////////

    CreateSection(32, "Intro", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysIntro  },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassIntro  },
      //{ chSynth, Loop,  seqSynth1     },
    });

    CreateSection(64, "Middle", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysVerse  },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassVerse  },
        { chSynth, Loop,  seqSynth1     },
    });

    CreateSection(32, "End", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysEnd    },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassEnd    },
    });
}
