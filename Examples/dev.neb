
///// Include other lib files. /////
#import "utils.neb"
#import "scale.neb"

// TODO - fills, intros, like that.


///// IO device names.
string MIDI_IN = "MIDI:MPK mini";
string MIDI_OUT = "MIDI:loopMIDI Port 1";
string OSC_IN = "OSC:6448";
string OSC_OUT = "OSC:127.0.0.1:1234";
string GS_MIDI_OUT = "MIDI:Microsoft GS Wavetable Synth";

///// Volumes
const double KEYS_VOL = 0.8;
const double DRUM_VOL = 0.8;

///// Variables
NVariable vColor;
NVariable vModN;
NVariable vVkeyNote;

NVariable vCtl2;
NVariable vCtl3;
NVariable vMod1;
NVariable vKeyNote;

///// Channels
NChannel chKeys;
NChannel chBass;
NChannel chDrums;
NChannel chSynth;

///// Sequences
NSequence seqDynamic;


////// Simple variables for functions
double[] _scaleNotes;
double[] _chordNotes;
int _noteNum = 48;

// Use wingm and virtual kbd only.
bool _local = true;


////// Nebulator midi stuff
public override void Setup()
{
    // All the neb variables.
    vColor = CreateVariable("color", 200, 0, 255); // change color
    vModN = CreateVariable("modn", 0, -10, 10);
    vVkeyNote = CreateVariable("note", 60, -127, 127, On_vVkeyNote); // note neg number for note off encoding
    vCtl2 = CreateVariable("ctl2", 200, 0, 255, On_vCtl2);
    vCtl3 = CreateVariable("ctl3", 200, 0, 255, On_vCtl3);
    vMod1 = CreateVariable("mod", 127, 0, 255, On_vMod1);

    // IO devices.
    if(_local)
    {
        // All the outputs.
        chKeys = CreateChannel("keys", GS_MIDI_OUT, 1, 0.1);
        chBass = CreateChannel("bass", GS_MIDI_OUT, 2, 0.1);
        chSynth = CreateChannel("synth", GS_MIDI_OUT, 3, 0.1);
        chDrums = CreateChannel("drums", GS_MIDI_OUT, 10, 0.1);

        // All the inputs.
        CreateController("VKEY:", 1, NoteControl, vVkeyNote);
    }
    else
    {
        // All the outputs.
        chKeys = CreateChannel("keys", MIDI_OUT, 1, 0.1);
        chBass = CreateChannel("bass", MIDI_OUT, 2, 0.1);
        chSynth = CreateChannel("synth", MIDI_OUT, 3, 0.1);
        chDrums = CreateChannel("drums", MIDI_OUT, 10, 0.1);

        // All the inputs.
        // CreateController(MIDI_IN, 1, 1, vMod1); // modulate eq
        // CreateController(MIDI_IN, 1, 2, vCtl2); // since I don't have a pitch knob, I'll use this instead
        // CreateController(MIDI_IN, 1, 3, vCtl3); // another controller
        // CreateController(MIDI_IN, 1, 4, BACK_COLOR); // change ui color
        // CreateController(MIDI_IN, 1, NoteControl, vKeyNote);
    }

    // Create set of notes (chord)
    CreateNotes("MY_CHORD", "1 -4 6 -b13");
    CreateNotes("MY_SCALE", "1 3 4 b7");

    // levers
    CreateLever(vColor);
    CreateLever(vModN);

    // CreateController("VKEY:", 0, NoteControl, vVkeyNote);

    // Get some stock chords and scales
    _scaleNotes = GetScaleNotes("Algerian", "A3");
    _chordNotes = GetChordNotes("C4.o7");
}

public override void Setup2()
{
    if(_local)
    {
        SendPatch(chKeys, AcousticGrandPiano);
        SendPatch(chBass, AcousticBass);
        SendPatch(chSynth, Lead1Square);
        SendPatch(chDrums, 32); // kit
    }

    BuildComp();

    // Could do a pan:
    // SendController(chKeys, Pan, 30);
}

void BuildComp()
{

    /////////////////////// sequences /////////////////////////////////

    NSequence seqKeysVerse = CreateSequence(8, new NSequenceElements
    {
        { "|7-      7-      |7-      7-      |", "G4.m7", KEYS_VOL },
        { "|    4-      4-8-|    4-      4-8-|", "G4.m6", KEYS_VOL*0.9 }
    });

    NSequence seqKeysChorus = CreateSequence(8, new NSequenceElements
    {
        { 0.0, "F4",    0.7,      0.2 },
        { 0.2, "D#4",   KEYS_VOL, 0.2 },
        { 1.0, "C4",    0.7,      0.2 },
        { 1.2, "B4.m7", 0.7,      0.2 },
        { 2.0, "F5",    0.7,      0.2 },
        { 2.2, "D#5",   KEYS_VOL, 0.2 },
        { 3.0, "C5",    0.7,      0.2 },
        { 3.2, "B5.m7", 0.7,      0.2 },
        { 4.0, "F3",    0.7,      0.2 },
        { 4.2, "D#3",   KEYS_VOL, 0.2 },
        { 5.0, "C3",    0.7,      0.2 },
        { 5.2, "B3.m7", 0.7,      0.2 },
        { 6.0, "F2",    0.7,      0.2 },
        { 6.2, "D#2",   KEYS_VOL, 0.2 },
        { 7.0, "C2",    0.7,      0.2 },
        { 7.2, "B2.m7", 0.7,      0.2 },
    });

    NSequence seqDrumsSimple = CreateSequence(8, new NSequenceElements
    {
        { 0.0, AcousticBassDrum,  DRUM_VOL },
        { 1.0, RideCymbal1,       DRUM_VOL },
        { 1.1, RideCymbal1,       DRUM_VOL },
        { 1.2, HiMidTom,          DRUM_VOL },
        { 2.0, AcousticBassDrum,  DRUM_VOL },
        { 3.0, RideCymbal1,       DRUM_VOL },
        { 3.1, RideCymbal1,       DRUM_VOL },
        { 4.0, AcousticBassDrum,  DRUM_VOL },
        { 5.0, RideCymbal1,       DRUM_VOL },
        { 5.1, RideCymbal1,       DRUM_VOL },
        { 5.2, HiMidTom,          DRUM_VOL },
        { 6.0, AcousticBassDrum,  DRUM_VOL },
        { 7.0, CrashCymbal2,      DRUM_VOL },
    });

    NSequence seqDrumsVerse = CreateSequence(8, new NSequenceElements
    {
        { "|8       8       |8       8       |", AcousticBassDrum, DRUM_VOL },
        { "|    8       8 8 |    8       8 8 |", AcousticSnare,    DRUM_VOL*0.9 },
        { "|      88      88|      88      88|", ClosedHiHat,      DRUM_VOL*1.1 },
    });

    NSequence seqBassVerse = CreateSequence(8, new NSequenceElements
    {
        { 0.0, "C2",  0.75, 0.2 },
        { 0.2, "C2",  0.75, 0.1 },
        { 3.3, "E2",  0.75, 0.2 },
        { 4.0, "C2",  0.75, 1.0 },
        { 7.2, "A#2", 0.75, 0.1 }
    });

    NSequence seqAlgo = CreateSequence(4, new NSequenceElements
    {
        { 1.2, AlgoDynamic, 0.8 },
    });

    NSequence seqEmpty = CreateSequence(4, new NSequenceElements
    {
    });

    // Sequence triggered in script.
    seqDynamic = CreateSequence(8, new NSequenceElements
    {
        { 0.0, "G3 ", 0.75, 0.2 },
        { 1.0, "A3 ", 0.75, 0.2 },
        { 2.0, "Bb3", 0.75, 0.2 },
        { 3.0, "C4 ", 0.75, 0.2 },
    });


    /////////////////////// sections /////////////////////////////////

    CreateSection(32, "Beginning", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysVerse   },
        { chDrums, Loop,  seqDrumsSimple },
        { chBass,  Loop,  seqBassVerse   }
    });

    CreateSection(16, "Middle", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysChorus },
        { chDrums, Loop,  seqDrumsVerse },
        { chBass,  Loop,  seqBassVerse  },
        { chSynth, Once,  seqAlgo,  seqEmpty, seqDynamic, seqEmpty }
    });

    CreateSection(32, "Ending", new NSectionElements
    {
        { chKeys,  Loop,  seqKeysVerse   },
        { chDrums, Loop,  seqDrumsSimple },
        { chBass,  Loop,  seqBassVerse   }
    });
}

////////////////// overrides /////////////////////////////

public override void Step()
{
    if(Tick != 0) // not looking at Ticks right now
    {
    }
}

////////////////// my functions /////////////////////////////

void AlgoDynamic()
{
    // Print("!!!! AlgoDynamic");
    int notenum = Random(55, 70);
    SendNote(chSynth, notenum, 0.8, 0.2);
}

void AlgoNull()
{
    // Print("!!!! AlgoNull");
}

////////////// event handlers ////////////////////

public void On_vCtl2()
{
    int newval = (int)MathUtils.Map(vCtl2.Value, 0, 127, 0, 16383);
    //Print("On_vCtl2:", newval);
    SendController(chSynth, PitchControl, newval);
}

public void On_vCtl3()
{
    //Print("On_vCtl3:", vCtl3.Value);
    if(Random(0, 10) == 0)
    {
        SendNote(chKeys, 50 + vCtl3.Value % 20, 0.7, 2.0);
    }
}

public void On_vMod1()
{
    //Print(vMod1.Name, "changed to", vMod1.Value);
    // Send a controller change.
    SendController(chSynth, Modulation, vMod1.Value);
}

public void On_vVkeyNote()
{
    //Print("vVkeyNote", "=", vVkeyNote.Value);

    // Test for note on or off.
    if(vVkeyNote.Value > 0)
    {
        SendNoteOn(chSynth, vVkeyNote.Value, 0.9);
    }
    else
    {
        SendNoteOff(chSynth, -vVkeyNote.Value);
    }
}
